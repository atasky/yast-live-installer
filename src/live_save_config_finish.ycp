/**
 * File:
 *  save_config_finish.ycp
 *
 * Module:
 *  Step of base installation finish
 *
 * Authors:
 *  Jiri Srain <jsrain@suse.cz>
 *
 * $Id: save_config_finish.ycp 36694 2007-03-05 14:17:54Z locilka $
 *
 */

{

textdomain "live-installer";

import "Progress";
import "Timezone";
import "Keyboard";
import "Installation";
import "Language";
import "FileUtils";

any ret = nil;
string func = "";
map param = $[];

/* Check arguments */
if(size(WFM::Args()) > 0 && is(WFM::Args(0), string)) {
    func = (string)WFM::Args(0);
    if(size(WFM::Args()) > 1 && is(WFM::Args(1), map))
	param = (map)WFM::Args(1);
}

y2milestone ("starting save_config_finish");
y2debug("func=%1", func);
y2debug("param=%1", param);

if (func == "Info")
{
    return (any)$[
	"steps" : 3,
	"when" : [ `live_installation ],
    ];
}
else if (func == "Write")
{
    // progress step title
    Progress::Title (_("Saving language..."));
    Language::Save();
    Progress::NextStep ();

    // progress step title
    Progress::Title (_("Saving keyboard configuration..."));
    Keyboard::Save();
    Progress::NextStep ();

     // progress step title
    Progress::Title (_("Saving time zone..."));
    Timezone::Save();

    // bnc#550874
    // Call a script if it exists
    string scriptname = "/usr/bin/correct_live_for_reboot";
    if (FileUtils::Exists (scriptname)) {
        y2milestone ("Calling %1 returned %2",
            scriptname,
            SCR::Execute (.target.bash_output, scriptname)
        );
        y2milestone ("Removing %1 returned %2",
            scriptname,
            SCR::Execute (.target.bash_output, sformat ("/bin/rm %1", scriptname))
        );
    } else {
        y2milestone ("Script %1 doesn't exist, skipping...", scriptname);
    }


}
else
{
    y2error ("unknown function: %1", func);
    ret = nil;
}

y2debug("ret=%1", ret);
y2milestone("save_config_finish finished");
return ret;


} /* EOF */
